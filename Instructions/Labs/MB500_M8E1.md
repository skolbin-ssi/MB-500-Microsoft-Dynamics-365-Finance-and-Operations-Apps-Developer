---
lab:
    title: 'Exercise 01: Create a custom business event'
    module: 'Module 08: Integration'
---

**MB-500: Microsoft Dynamics 365: Finance and Operations Apps Developer**


Change Record
=============

| Version | Date        | Change                                                                                                                                                                                           |
|---------|-------------|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
| 1.0     | 30 Apr 2021 | Initial release                                                                                                                                                                                  |


Scenario
========

In this lab, a custom business event will be created that can capture the event
when a new insurance is added for a fixed asset. It can also capture the Vendor
email, so that email can be sent to the vendor.

Lab solution
============

Prerequisite
------------

-   To execute this exercise you need to have a Dynamics 365 Finance and
    Operations *apps* development environment with access to *Visual Studio*

-   Import the following lab exercise as a prerequisite of this lab (for import
    instructions, please see appendix)

-   MB500TrainingProject.axpp

-   MB500TrainingProjectDataModel.axpp

-   MB500TrainingProjectUI.axpp

Task \#1: Create a custom business event
----------------------------------------

1.  Right click on the Training solution under Solution Explorer.

2.  Click on Add \> New project and create a new project
    **MB500TrainingProjectBusinessEvent**.


    *A new method is added in the VendTable table, which will return the email
    address of the vendor*

3.  Right-click on the Project **MB500TrainingProjectBusinessEvent** and go to
    Add \> New Item

4.  Select Class under Code to create a new class
    **MBTVendTableTable_Extension**

5.  Change the signature for the class **MBTVendTableTable_Extension** to:

		[ExtensionOf(tableStr(VendTable))]
		final class MBTVendTableTable_Extension
		{
		}

6.  Add a new method getEmail() to find the Email of the vendor. This is the
    code:

		public Email getEmail()
		{
			DirPartyLocation dirPartyLocation;
			LogisticsElectronicAddress logisticsElectronicAddress;
			LogisticsElectronicAddressRole logisticsElectronicAddressRole;
			LogisticsLocationRole logisticsLocationRole;
			Email emailValue;
			while select dirPartyLocation
				where dirPartyLocation.party == this.Party
			{
				while select logisticsElectronicAddress
					where logisticsElectronicAddress.Location == dirPartyLocation.Location
					&& logisticsElectronicAddress.Type == LogisticsElectronicAddressMethodType::Email
				{
					while select logisticsElectronicAddressRole
						where logisticsElectronicAddressRole.ElectronicAddress == logisticsElectronicAddress.RecId
					join logisticsLocationRole
						where logisticsLocationRole.RecId == logisticsElectronicAddressRole.LocationRole
					{
						if(!emailValue)
						{
							emailValue = logisticsElectronicAddress.Locator;
						}
					}
				}
			}
			return emailValue;
		}


    *A new data contract is added that will capture the data to be sent while
    the insert event will be triggered.*

7.  Right-click on the Project **MB500TrainingProjectBusinessEvent** and go to
    Add \> New Item

8.  Select Class under Code to create a new class
    **MBTAssetInsuranceBusinessEventContract**

9.  Use the following signature for the class **MBTAssetInsuranceBusinessEventContract:**

		/// <summary>
		/// The data contract for a <c>AssetInsurancePostedBusinessEvent</c>.
		/// </summary>
		[DataContract]
		public final class MBTAssetInsuranceBusinessEventContract extends
		BusinessEventsContract
		{
		}

10. Add the member variables in the contract class **MBTAssetInsuranceBusinessEventContract**.

		private AssetId AssetId;
		private MBTAssetInsuranceId AssetInsuranceId;
		private VendAccount VendAccount;
		private Email VendEmail;
		private String10 InsurancePolicyDate;
		private AssetInsurancePolicyNum AssetInsurancePolicyNum;
		private AssetInsuredValue AssetInsuredValue;
		private AssetPolicyAmount PremiumAmount;
		private MBTPeriod PaymentPeriod;
		private AssetInsuranceAgent InsuranceAgent;
		private LegalEntityDataAreaId legalEntity;

11. Add the default constructor in the contract class  **MBTAssetInsuranceBusinessEventContract**.

		private void new()
		{
		}

12. Add parm methods for all the member variables

		[DataMember('AssetId'), BusinessEventsDataMember("Asset ID")]
		public AssetId parmAssetId(AssetId _assetId = AssetId)
		{
		AssetId = _assetId;
		return AssetId;
		}

		[DataMember('AssetInsuranceId'), BusinessEventsDataMember("Asset Insurance ID")]
		public MBTAssetInsuranceId parmAssetInsuranceId(MBTAssetInsuranceId _assetInsuranceId = AssetInsuranceId)
		{
		AssetInsuranceId = _assetInsuranceId;
		return AssetInsuranceId;
		}

		[DataMember('VendAccount'), BusinessEventsDataMember("Vendor Account")]
		public VendAccount parmVendAccount(VendAccount _vendAccount = VendAccount)
		{
		VendAccount = _vendAccount;
		return VendAccount;
		}

		[DataMember('VendEmail'), BusinessEventsDataMember("Vendor Email")]
		public Email parmVendEmail(Email _vendEmail = VendEmail)
		{
		VendEmail = _vendEmail;
		return VendEmail;
		}

		[DataMember('InsurancePolicyDate'), BusinessEventsDataMember("Insurance Policy Date")]
		public String10 parmPolicyDate(String10 _insurancePolicyDate = InsurancePolicyDate)
		{
		InsurancePolicyDate = _insurancePolicyDate;
		return InsurancePolicyDate;
		}

		[DataMember('AssetInsurancePolicyNum'), BusinessEventsDataMember("Policy Number")]
		public AssetInsurancePolicyNum parmPolicyNum(AssetInsurancePolicyNum _policyNum = AssetInsurancePolicyNum)
		{
		AssetInsurancePolicyNum = _policyNum;
		return AssetInsurancePolicyNum;
		}

		[DataMember('AssetInsuredValue'), BusinessEventsDataMember("Insured Value")]
		public AssetInsuredValue parmInsuredValue(AssetInsuredValue _InsuredValue = AssetInsuredValue)
		{
		AssetInsuredValue = _InsuredValue;
		return AssetInsuredValue;
		}

		[DataMember('PremiumAmount'), BusinessEventsDataMember("Premium Amount")]
		public AssetPolicyAmount parmPremiumAmount(AssetPolicyAmount _premiumAmount = PremiumAmount)
		{
		PremiumAmount = _premiumAmount;
		return PremiumAmount;
		}

		[DataMember('PaymentPeriod'), BusinessEventsDataMember("Payment Period")]
		public MBTPeriod parmPaymentPeriod(MBTPeriod _paymentPeriod = PaymentPeriod)
		{
		PaymentPeriod = _paymentPeriod;
		return PaymentPeriod;
		}

		[DataMember('InsuranceAgent'), BusinessEventsDataMember("Insurance Agent")]
		public AssetInsuranceAgent parmInsuranceAgent(AssetInsuranceAgent _insuranceAgent = InsuranceAgent)
		{
		InsuranceAgent = _insuranceAgent;
		return InsuranceAgent;
		}

		[DataMember('LegalEntity'), BusinessEventsDataMember("Legal Entity")]
		public LegalEntityDataAreaId parmLegalEntity(LegalEntityDataAreaId _legalEntity = legalEntity)
		{
		legalEntity = _legalEntity;
		return legalEntity;
		}


13. Add a new method **initialize** to initialize the value of the member
    variables of the contract class **MBTAssetInsuranceBusinessEventContract**

		private void initialize(MBTAssetInsurance _assetInsurance)
		{
			AssetId = _assetInsurance.AssetId;
			AssetInsuranceId = _assetInsurance.AssetInsuranceId;
			VendAccount = _assetInsurance.VendAccount;
			VendEmail = VendTable::find(_assetInsurance.VendAccount).getEmail();
			InsurancePolicyDate = date2Str(_assetInsurance.InsurancePolicyDate, 213, DateDay::Digits2, DateSeparator::Slash, DateMonth::Digits2, DateSeparator::Slash, DateYear::Digits4);
			AssetInsurancePolicyNum = _assetInsurance.InsurancePolicyNum;
			AssetInsuredValue = _assetInsurance.InsuredValue;
			PremiumAmount = _assetInsurance.PremiumAmount;
			PaymentPeriod = _assetInsurance.Period;
			InsuranceAgent = _assetInsurance.InsuranceAgent;
		}

14. Add a new method **newAssetInsurance** in the contract class
    **MBTAssetInsuranceBusinessEventContract** as follows:

		public static MBTAssetInsuranceBusinessEventContract newAssetInsurance(MBTAssetInsurance _assetInsurance)
		{
			var contract = new MBTAssetInsuranceBusinessEventContract();
			contract.initialize(_assetInsurance);
			return contract;
		}


    *A new business event class is added that will capture the insert event on
    the Asset Insurance table.*

15. Right-click on the Project **MB500TrainingProjectBusinessEvent** and go to
    Add \> New Item

16. Select Class under Code to create a new class
    **MBTAssetInsurancePostedBusinessEvent**

17. Use the following signature for the class
    **MBTAssetInsurancePostedBusinessEvent:**

		/// <summary>
		/// Asset Insurance posted business event.
		/// </summary>
		[BusinessEvents(classStr(MBTAssetInsuranceBusinessEventContract), 'Asset Insurance', 'Asset Insurance Description', ModuleAxapta::FixedAssets)]
		public final class MBTAssetInsurancePostedBusinessEvent extends BusinessEventsBase
		{
		}

18. Add the member variable in the class
    **MBTAssetInsurancePostedBusinessEvent**.

		MBTAssetInsurance AssetInsurance;


19. Add the following method in the class
    **MBTAssetInsurancePostedBusinessEvent:**

		[Wrappable(true), Replaceable(true)]
		public BusinessEventsContract buildContract()
		{
			return MBTAssetInsuranceBusinessEventContract::newAssetInsurance(AssetInsurance);
		}

20. Add the default constructor in the class
    **MBTAssetInsurancePostedBusinessEvent**:

		private void new()
		{
		}

21. Add a parm method for the member variable in the class
    **MBTAssetInsurancePostedBusinessEvent.**

		private MBTAssetInsurance parmAssetInsurance(MBTAssetInsurance _assetInsurance = AssetInsurance)
		{
			AssetInsurance = _assetInsurance;
			return AssetInsurance;
		}

22. Add the following new method to trigger the business event in the class
    **MBTAssetInsurancePostedBusinessEvent**

    *Create an extension of the Asset Insurance table and add a chain of command
    of the insert() method to capture the insert trigger.*

		/// <summary>
		/// Creates a <c>AssetInsurancePostedBusinessEvent</c> from a <c>MBTAssetInsurance</c> record.
		/// </summary>
		/// <param name = "_assetInsurance"> A <c>MBTAssetInsurance</c> record.</param>
		/// <returns>A <c>MBTAssetInsurancePostedBusinessEvent</c>.</returns>
		public static MBTAssetInsurancePostedBusinessEvent newAssetInsurance(MBTAssetInsurance _assetInsurance)
		{
			MBTAssetInsurancePostedBusinessEvent businessEvent = new MBTAssetInsurancePostedBusinessEvent();
			businessEvent.parmAssetInsurance(_assetInsurance);
			return businessEvent;
		}

23. Right-click on the Project **MB500TrainingProjectBusinessEvent** and select
    Add \> New Item

24. Select Class under Code to create a new class
    **MBTAssetInsuranceTable_Extension**

25. Use the following signature for the class
    **MBTAssetInsuranceTable_Extension:**

		[ExtensionOf(tableStr(MBTAssetInsurance))]
		final class MBTAssetInsuranceTable_Extension
		{
		}

26. Add the following code within the brackets to create a chain of command for
    the insert() method:

		public void insert()
		{
			next insert();
			if (BusinessEventsConfigurationReader::isBusinessEventEnabled(classStr(MBTAssetInsurancePostedBusinessEvent)))
			{
				MBTAssetInsurancePostedBusinessEvent::newAssetInsurance(this).send();
			}
		}

27. Save and Build the project **MB500TrainingProjectBusinessEvent**

Output 

1.  Navigate to Dynamics 365 Finance and Operations apps in your browser

2.  Navigate to **System administration** \> **Setup** \> **Business events** \>
    **Business events catalog**

3.  Select **Manage** \> **Rebuild business event catalog**

4.  You will find a new business event is created named
    **MBTAssetInsurancePostedBusinessEvent** under the **Fixed Asset** category

Appendix 

**Project Import instructions:**  

-  The lab resources can be found by opening the **File explorer** located on the **Taskbar**. then selecting the **Documents** folder and finally selecting the **Course Assets** folder.

-   To import a project, navigate to Visual Studio and select **Dynamics 365**
    \> **Import Project** 

-   In the File name field, select the file name with path of the .axpp file
    that you want to import 

-   For the first import, of MB500TrainingProject, select **New solution**. For
    subsequent ones, select the **Current solution** option.

-   Select the **OK** button. You may safely override if prompted.
